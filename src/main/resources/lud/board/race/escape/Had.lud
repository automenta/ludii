(define "SiteToMoveOnTrack" (trackSite Move steps:(count Pips)))

//------------------------------------------------------------------------------

(game "Had"
    (players 2)  
    (equipment 
        { 
        (board (rectangle 3 14) 
            {    
            (track "Track1" "0,E,N1,W,N1,E" P1 directed:true)
            (track "Track2" "41,W,S1,E,S1,W" P2 directed:true)
            }
        )
        (dice d:4 num:2)
        (piece "Ball" Each 
            (if (= ("SiteToMoveOnTrack") Off)
                (move
                    Remove
                    (from) 
                )
                (move 
                    (from) 
                    (to 
                        ("SiteToMoveOnTrack") 
                        if:(is Empty (to)) 
                    )
                )
            )
        ) 
        }
    )  
    
    (rules 
        (start 
            { 	
            (place "Ball1" (sites Bottom))
            (place "Ball2" (sites Top))
            }
        )	
        
        (play 
            (do 
                (roll) 
                next:(forEach Piece)
            )
        )
        
        (end 
            (if 
                ("NoPiece" Mover) 
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Had is a race game played in the Bordj Bou Arreridj region of Algeria in the early twentieth century. It resembles other games such as Tab which are popular in North Africa and Southwest Asia.")
        (rules "3x14 board. Fourteen pieces per player, placed in the outer rows of the board. Two knucklebones used as dice. Players move their pieces according to the throws of the knucklebones. Pieces move in a boustrophedon path along the board. The first player to remove their pieces from the board wins.")
        (source "Robert 1921: 66.")
        (version "1.0.8")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in Algeria, around 1921CE.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
)

