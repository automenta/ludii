(define "DieNotUsed" (!= (pips) 0))

(define "SiteToSelect" (trackSite Move from:(mapEntry "StartTrack" (mover)) steps:(- (pips) 1)))

(define "SiteToMoveOnTrack" (trackSite Move steps:(pips)))

(define "RemoveAPiece" (move Remove (from) ) )

(define "PieceUnstacked" (= (state at:#1) 1) )
(define "PieceStacked" (= (state at:#1) 0) )
(define "Unstack" (set State at:(last To) 1))
(define "Stack" (set State at:(last To) 0))

//------------------------------------------------------------------------------

(game "Ofanfelling"
    (players 2)
    (equipment {
        (board 
            (merge {
                (rectangle 1 6) 
                (shift 7 0 (rectangle 1 6) )
                (shift 0 6 (rectangle 1 6) )
                (shift 7 6 (rectangle 1 6) )
            })
            {
            (track "Track1" {6..11} P1 directed:true)
            (track "Track2" {17..12} P2 directed:true)
            }
            use:Vertex
        )
        (dice d:6 num:2)
        (piece "Disc" Each
            (forEach Die 
                if:("DieNotUsed")
                (if (= "SiteToMoveOnTrack" Off)
                    "RemoveAPiece"
                    (move 
                        (from) 
                        (to 
                            "SiteToMoveOnTrack" 
                        )
                    )
                )
            )
        )
        (map "StartTrack" {(pair P1 6) (pair P2 17)})
    })
    (rules 
        (start {
            (place Stack "Disc1" (sites {6..11}) count:2)
            (place Stack "Disc2" (sites {12..17}) count:2)
        })
        phases:{
        (phase "UnStacking" (play 
                (do 
                    (if (or (is Pending) (not "SameTurn")) (roll)) 
                    next:
                    (forEach Die 
                        if:("DieNotUsed")
                        (if ("PieceStacked" "SiteToSelect")
                            (move select (from ("SiteToSelect"))
                                (then 
                                    (and {
                                        ("Unstack")
                                        (if (not (all DiceUsed))
                                            (moveAgain)
                                        )
                                    })
                                )
                            )
                        )
                    )
                    (then (if (all DiceEqual) (and (set Pending) (moveAgain))))
                )
            )
            (nextPhase Mover (= 0 (count Sites in:(forEach (sites Occupied by:Mover) if:("PieceStacked" (site))))) "Stacking")
        )
        (phase "Stacking" (play 
                (do 
                    (if (or (is Pending) (not "SameTurn")) (roll)) 
                    next:
                    (forEach Die 
                        if:("DieNotUsed")
                        (if ("PieceUnstacked" "SiteToSelect")
                            (move select (from ("SiteToSelect"))
                                (then 
                                    (and {
                                        ("Stack")
                                        (if (not (all DiceUsed))
                                            (moveAgain)
                                        )
                                    })
                                )
                            )
                        )
                    )
                    (then (if (all DiceEqual) (and (set Pending) (moveAgain))))
                )
            )
            (nextPhase Mover (= 0 (count Sites in:(forEach (sites Occupied by:Mover) if:("PieceUnstacked" (site))))) "BearingOff")
        )
        (phase "BearingOff" (play 
                (do 
                    (if (or (all DiceEqual) (not "SameTurn")) (roll)) 
                    next:(forEach Piece)
                    (then (if (or (not (all DiceUsed)) (all DiceEqual)) (moveAgain)))
                )
            )
            (end (if ("NoPiece" Mover) (result Mover Win)))
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Ofanfelling is a race game played in Iceland. Though it is played on the same board as other European Tables Games, it is different from most of them in that all of the game play for each player takes place in separate quadrants of the board, and the players' pieces never interact with one another.")
        (aliases {"Ofanfellingartafl"})
        (rules "2x12 board, with spaces as points, divided in two. Twelve pieces per player, which start stacked, two per point in the quadrant to the player's right. Two six-sided dice. Players unstack the pieces by moving pieces off of the stack, but remaining on the same point, according to the throws of the dice. The value of the throw indicates the number of the point which can be unstacked. For example, a throw of 1 and 4 allows the player to unstack the pieces on points 1 and 4. Doubles allow players to only move one piece, but grant the player another throw. When all of a player's pieces are unstacked, they must be restacked, in the same way. When they have been successfully restacked, the player then bears off the pieces from this quadrant of the board. When bearing off, players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. The first player to bear off all their pieces wins. ")
        (source "Fiske 1905: 353.")
        (version "1.0.8")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in Iceland, around 1905CE.")
        }
    )
    
    (graphics {
        (show Edges Hidden)
        (board Style Table)
        (stackType state:1 Backgammon)
    })
    
)

