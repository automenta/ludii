(define "ThrowValue" (mapEntry "ThrowDiceValue" (count Pips)))

(define "SiteToMoveOnTrack" 
    (if (= 0 (state at:(from)))
        (trackSite Move "TrackCW" steps:("ThrowValue"))
        (trackSite Move "TrackCCW" steps:("ThrowValue"))
    )
)

(define "ChangeOfTrack" 
    (if (= 0 (state at:(last To)))
        (set State at:(last To) 1)
        (set State at:(last To) 0)
    )
)

(define "EndSiteOfTrack" 
    (if (= 0 (state at:(last To)))
        (trackSite EndSite Mover "TrackCW")
        (trackSite EndSite Mover "TrackCCW")
    )
)

(define "CaptureEnemyPiece" 
    (apply 
        if:(is Enemy (who at:(to))) 
        (remove (to))
    ) 
)

(define "BigHole" 0)

//------------------------------------------------------------------------------

(game "Mapuche Game"
    (players 2)
    (equipment {
        (board
            (remove
                (circle {26})
                vertices:{ 0 12..16}
            )
            {
            (track "TrackCW1" "11,12,13,14,15,16,17,18,19,20,0,1,2,3,4,5,6,7,8,9,10" P1 directed:true)
            (track "TrackCCW1" "10,9,8,7,6,5,4,3,2,1,0,20,19,18,17,16,15,14,13,12,11" P1 directed:true)
            (track "TrackCW2" "11,12,13,14,15,16,17,18,19,20,0,1,2,3,4,5,6,7,8,9,10" P2 directed:true)
            (track "TrackCCW2" "10,9,8,7,6,5,4,3,2,1,0,20,19,18,17,16,15,14,13,12,11" P2 directed:true)
            }
            use:Vertex
        )
        (regions "AllSitesExceptTop" (difference (sites Board) 0))
        (piece "Ball" Each
            (if (!= "SiteToMoveOnTrack" Off)
                (if (or
                        (is In "SiteToMoveOnTrack" (sites Empty))
                        (is Enemy (who at:"SiteToMoveOnTrack"))
                    )
                    (move 
                        (from)
                        (to 
                            "SiteToMoveOnTrack" 
                            "CaptureEnemyPiece"
                        )
                    )
                )
            )
        )
        (dice d:2 from:0 num:4)
        (map "ThrowDiceValue" {(pair 0 4) (pair 1 0) (pair 2 2) (pair 3 0) (pair 4 3)})
        (hand Each)
    })
    (rules 
        (start {
            (place "Ball1" (sites {1..10}) state:1)
            (place "Ball2" (sites {11..20}))
        })
        (play 
            (do  
                (roll) 
                next:(if (can Move (if (!= ("ThrowValue") 0) (forEach Piece)))
                    (forEach Piece (then (if (!= ("ThrowValue") 0) (moveAgain))))
                    (move Pass (then (if (!= ("ThrowValue") 0) (moveAgain))))
                )
                (then 
                    (and
                        (if (and (not (is Empty "BigHole")) (!= "BigHole" (last To)))
                            (remove "BigHole")
                        )
                        (if (= (last To) ("EndSiteOfTrack"))
                            ("ChangeOfTrack")
                        )
                    )
                )
            )
        )
        (end {
            (if ("NoPiece" P1) (result P2 Win))
            (if ("NoPiece" P2) (result P1 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "This game was played in during the sixteenth century by the Mapuche people of South America, but its name is not given in the sources. It is a race game with captures that is similar to other games from South America which were played in the nineteenth and twentieth centuries.")
        (rules "21 holes, arranged in an arc, the central hole larger than the others. Ten pieces per player, each player's pieces beginning on one side of the board, one in each hole. Four beans used as dice, with a black side and a white side. The throws are as follows: Four white sides up = 4; four black sides up = 3; two white/two black up = 2; three white or three black = player loses their turn. Players continue to throw until they lose their turn. If a piece lands in the central hole, and it cannot move from it on its next opportunity, it is removed from the game. Pieces moving into the opponent's side of the board capture any of the opponent's pieces which occupy a space on which they land. When a piece reaches the end of the track, they proceed back in the other direction. The player who captures all ten of the opponent's pieces wins.")
        (source "Depaulis 2003: 90.")
        (version "1.0.8")
        (classification "board/war")
        (credit "Eric Piette")
        (origin  "This game was played in South America, from around 1525CE to 1558CE.")
        }
    )
    
    (graphics {
        (show Symbol "disc" "AllSitesExceptTop" Vertex fillColour:(colour White) edgeColour:(colour Black) scale:0.8)
        (show Symbol "disc" Vertex 0 fillColour:(colour White) edgeColour:(colour Black) scale:1.3)
        (piece Colour "Die" state:1 fillColour:(colour Black))
    })
    
)

