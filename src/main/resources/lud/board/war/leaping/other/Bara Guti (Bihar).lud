(game "Bara Guti (Bihar)"  
    (players 2)  
    (equipment { 
        (board (circle {8 8 8}) use:Vertex)
        (piece "Counter" Each 
            (or 
                ("HopRotationalSequenceCapture") 
                ("StepRotationalToEmpty") 
            )
        )
    })  
    
    (rules 
        (start {
            (place "Counter1" (sites {20..23 12..15 4..7})  )
            (place "Counter2" (sites {8 16 24 1..3 9..11 17..19}) )
        })
        
        (play 
            (if "SameTurn"
                (or
                    ("HopRotationalSequenceCaptureAgain")	
                    (move Pass)
                )
                (forEach Piece)
            )
        )
        
        (end (if ("NoPiece" Next) (result Next Loss)))  
    )
) 

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Bara Guti is a term used for a couple of different board games in India. In the Bihar region, it is used for this circular game. ")
        (rules "Three concentric circles, with four diameters dividing it into eight equal sections. Twelve pieces per player, placed on the points on one half of the circle, leaving the central point open. Players alternate turns moving one of their pieces to an empty adjacent spot along the lines. A piece may capture an opponent's piece by hopping over it to an empty spot immediately on the opposite side of the opponent's piece along the lines. The player who captures all of the opponent's pieces wins.")
        (source "Das Gupta 1935: 409-410.")
        (version "1.0.8")
        (classification "board/war/leaping")
        (credit "Eric Piette")
        (origin  "This game was played in India, around 1935CE.")
        }
    )
    
)
