(define "ArmSize" <Board:armSize>)
(define "NumInitPiece" <Board:numPiece>)

//-----------------------------------------------------------

(game "Laram Wali"
    (players 2)
    (equipment
        {
        (board
            (merge
                (shift "ArmSize" 0 (rectangle (* 3 "ArmSize") "ArmSize"))
                (shift 0 "ArmSize" (rectangle "ArmSize" (* 3 "ArmSize")))
            )
        )
        (hand Each)
        (piece "Ball" Each
            (or
                (move 
                    Step Orthogonal
                    (to if:(is Empty (to))) 
                )
                (move Hop 
                    Orthogonal
                    (between 
                        (range 0 "NumInitPiece")
                        if:(is Enemy (who at:(between))) 
                        (apply (remove (between)))
                    )
                    (to if:(is Empty (to)))
                )
            )
        )
        }
    )
    (rules
        (start 
            {
            (place "Ball" "Hand" count:"NumInitPiece") 
            }
        )
        
        phases:{
        (phase "Placement"
            (play 
                (move 
                    (from (handSite Mover)) 
                    (to (difference (sites Empty) (expand (sites Centre) steps:(- (/ "ArmSize" 2) (if (is Even "ArmSize") 1 0)))))
                ) 
            )
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        (phase "Movement"
            (play 
                (forEach Piece)
            )
        )
        }
        
        (end (if ("NoPiece" Next) (result Mover Win)))
    )	
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <armSize> <numPiece> }
    {
    (item "1"     <1> <2> "The game is played on a cross board of arm size of 1.") 
    (item "2"     <2> <4> "The game is played on a cross board of arm size of 1.")  
    (item "3"     <3> <6> "The game is played on a cross board of arm size of 1.")**  
    (item "4"     <4> <8> "The game is played on a cross board of arm size of 1.")  
    (item "5"     <5> <10> "The game is played on a cross board of arm size of 1.")  
    (item "6"     <6> <12> "The game is played on a cross board of arm size of 1.")  
    (item "7"     <7> <14> "The game is played on a cross board of arm size of 1.")  
    (item "8"     <8> <16> "The game is played on a cross board of arm size of 1.")  
    (item "9"     <9> <18> "The game is played on a cross board of arm size of 1.")  
    (item "10"    <10> <20> "The game is played on a cross board of arm size of 1.")  
})

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Laram Wali is a game with captures played in Mali. It is played on a cross-shaped board, on which pieces are placed and then a capturing phase begins.")
        (aliases {"Padikoré"})
        (rules "Cross-shaped board, with any number of squares in the arm with a central square, and a number of pieces per player equal to the number of spaces in two arms. Three squares in each arm with six pieces per player is common. Players alternate turns placing pieces on the board. Players may not place pieces in the central space. When the pieces are placed, players alternate turns move a piece to am empty adjacent square, or hopping over an adjacent opponent's piece or an unbroken line of opponent's pieces with an empty space behind it. The player who captures all of the opponent's pieces wins.
        ")
        (source "Béart 1955: 461-462.")
        (version "1.0.8")
        (classification "board/war/leaping")
        (credit "Eric Piette")
        (origin  "This game was played in Mali, around 1955CE.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
)
