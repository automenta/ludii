(game "Lam Pusri"  
    (players 2)  
    (equipment { 
        (board 
            (merge 
                { 
                (shift 0 2 (square 5 diagonals:Alternating) ) 
                (wedge 3) 
                (shift 0 6 (rotate 180 (wedge 3))) 
                } 
            ) 
            use:Vertex
        )
        (piece "Ball" Each 
            (or 
                ("HopSequenceCapture")
                ("StepToEmpty")
            )
        )
    })  
    (rules 
        (start {
            (place "Ball1" (union {(sites Row 0) (sites Row 1) (sites Row 2) (sites Row 3) (expand (sites {"E5"}) W)}))
            (place "Ball2" (union {(sites Row 5) (sites Row 6) (sites Row 7) (sites Row 8) (expand (sites {"A5"}) E)}))
        })
        
        (play 
            (if "SameTurn"
                (or
                    ("HopSequenceCaptureAgain")	
                    (move Pass)
                )
                (forEach Piece)
            )
        )
        
        (end (if ("NoPiece" Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Lam Pusri is a game with leaping captures that was played in the Teesta River valley of India during the early twentieth century.")
        (aliases {"Sipahi Kat"})
        (rules "5x5 intersecting lines, with diagonals in each quadrant. Two triangles, the apexes of which intersect with the square at the midpoint of opposite sides. One line bisecting the base of the triangle, and another bisecting this line. Eighteen pieces per player, each side arranged on one side of the board, with the central point empty and the player's pieces on the points to the right of it. Players alternate turns moving one of their pieces to an empty point. A piece may jump an opponent's piece to capture it. Multiple captures are allowed. The player who captures all of the opponent's pieces wins.
        ")
        (source "Hora 1933: 10-11.")
        (version "1.0.8")
        (classification "board/war/leaping/alquerque")
        (credit "Eric Piette")
        (origin  "This game was played in India, around 1933CE.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
)

