(define "JumpToPromotionZone" (is In (last To) (sites Next)) )

//------------------------------------------------------------------------------

(game "Makvoer" 
    (players {(player N) (player S)})
    (equipment { 
        (board (square 8)) 
        (piece "Counter" Each 
            (or 
                "StepDiagonalToEmpty" 
                "HopDiagonalCapture"
                (then 
                    (if "JumpToPromotionZone"
                        (promote (last To) (piece "DoubleCounter") Mover)
                    )
                )
            )
        )
        (piece "DoubleCounter" Each
            (or
                (move Slide Diagonal)
                (move Hop 
                    (directions {FR FL})
                    (between 
                        before:(count Rows) 
                        after:(count Rows)
                        if:(is Enemy (who at:(between))) 
                        (apply (remove (between)))
                    )
                    (to if:(is Empty (to)))
                )
            )
        )
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
    })  
    
    (rules 
        (start { 
            (place "Counter1" (difference (expand (sites Bottom) steps:2) (sites Phase 1)))
            (place "Counter2" (difference (expand (sites Top) steps:2) (sites Phase 1)))
        })
        
        (play (forEach Piece)) 
        
        (end (if ("NoPiece" Next) (result Mover Win))) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Makvœr is a form of Draughts played in Denmark, known since the beginning of the nineteenth century. It introduces restrictions on the \"flying leap\" of promoted pieces.")
        (aliases {"Makvœr" "Makvar"})
        (rules "8x8 board, twelve pieces per player, arranged on the black spaces. Pieces move diagonally one space, or may hop an opponent's piece to capture it. When a piece reaches the opponent's edge of the board, the piece becomes a \"Dam,\" and may move diagonally any number of spaces, including to jump over one piece, but such a capture can only be made when moving in a forward direction. The player who captures all the opponent's pieces wins. ")
        (source "Jörgensen 1802: 339.")
        (version "1.0.8")
        (classification "board/war/leaping/draughts")
        (credit "Eric Piette")
        (origin  "This game was played in Denmark, around 1802CE.")
        }
    )
    
    (graphics {
        (board Style Chess)
        (piece Families {"Defined" "Isometric"})
    })
    
)
