(define "Columns" 16)

(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns") ) )

(define "OppositeOuterPit" (if (is Mover P1) (+ (to) (* "Columns" 2)) (- (to) (* "Columns" 2)) ) )

(define "PlayFromLastHole" (sites {(last To afterConsequence:true)}))

(define "MoveAgainAfterCapture" (and (is Pending) (is Mover Prev)))

(define "StylizedMove" (sites {(mapEntry "Opening" (mover))}))

(define "Sow" 
    (then 
        (sow
            "Track"
            owner:(mover)
            apply:(if (> (count at:(to)) 1)
                (moveAgain)
                (if (is In (to) (sites Mover "Inner"))
                    (and
                        {
                        (fromTo 
                            (from (to)) 
                            (to (handSite Mover)) 
                            count:(count at:(to)) 
                        )
                        (if (> (count at:"OppositePit") 0)
                            (fromTo 
                                (from "OppositePit") 
                                (to (handSite Mover)) 
                                count:(count at:"OppositePit")
                            ) 
                        ) 
                        (if (> (count at:"OppositeOuterPit") 0)
                            (fromTo 
                                (from "OppositeOuterPit") 
                                (to (handSite Mover)) 
                                count:(count at:"OppositeOuterPit")
                            ) 
                        )
                        (set Pending) // We indicate we can capture again in pending the next state.
                        (moveAgain)
                        }
                    )
                )
            )
        )
    )
)

(define "PiecesOwnedBy" (+ (count at:(handSite #1)) (count in:(sites #1 "Home"))))

//------------------------------------------------------------------------------

(game "Mefuvha"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "Track1" "0,E,N1,W" loop:true P1)
            (track "Track2" "32,E,N1,W" loop:true P2)
            }
        )
        (regions "Home" P1 (sites Track "Track1"))                              // P1 Home
        (regions "Home" P2 (sites Track "Track2"))                              // P2 home
        (regions "Inner" P1 (difference (sites Track "Track1") (sites Bottom))) // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "Track2") (sites Top) ))   // P2 inner Home
        (map "Opening" {(pair P1 18) (pair P2 45)})                             // Hole for the opening
        (piece "Seed" Shared)
        (hand Each)
        }
    )  
    (rules 
        
        (start { 
            (set Count 2 
                to:(difference 
                    (union (sites P1 "Home") (sites P2 "Home") ) 
                    (sites {"A2" "B2" "O3" "P3"})
                ) 
            ) 
            (set Count 1 to:(sites {"B2" "O3"}) )
        })
        
        (play 
            (if "MoveAgainAfterCapture"
                (move
                    Select
                    (from (sites Next "Home") if:(> (count at:(from)) 0))
                    (then 
                        (fromTo 
                            (from (last From)) 
                            (to (handSite Mover)) 
                            count:(count at:(last From)) 
                        )
                    )
                )
                (priority
                    (move Select 
                        (from 
                            (if 
                                (is Mover Prev) 
                                "PlayFromLastHole" 
                                (if 
                                    (< (count Turns) 3) 
                                    "StylizedMove" 
                                    (sites Mover "Home") 
                                ) 
                            ) 
                            if:(> (count at:(from)) 1)
                        )
                        "Sow"
                    )
                    (move 
                        Select 
                        (from 
                            (if 
                                (is Mover Prev) 
                                "PlayFromLastHole" 
                                (sites Mover "Home") 
                            )
                            if:(= (count at:(from)) 1)
                        )
                        "Sow"
                    )
                )
            )
        )
        
        (end
            (if (no Moves Mover) 
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1)) 
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Mefuhva is a four-row mancala-style board game played by the Venda people in South Africa. It is played by men on large boards or in hollows dug into the ground. Boards consist of four rows of six to twenty-eight holes; between sixteen to twenty is most common.")
        (rules "Two counters in each hole except the leftmost on the inner row, which is left empty, and the one to its right which contains one counter. Players sow by picking up the counters in any of their holes and sowing them in an anti-clockwise direction. Sowing continues when the last counter falls into an occupied hole by picking up the counters in that hole and continuing in the same direction. When the final counter lands in an empty hole in the inner row, the counters in the opponent's hole opposite in the inner row are captured; if there are also counters in the opponent's outer row opposite, these are also captured. The player is also entitled to capture counters in any other hole on the opponent's side. The turn ends with a capture and the opponent's turn begins. Play always begins with a stylized move, where the counters are taken from the third hole from the left in the inner row, sowing and making captures as described above. Players may not sow single counters unless there are no holes on their side containing multiple counters. Play ends when one player has captured all of their opponent's counters.")
        (source "Stayt 1931: 364-366.")
        (version "1.0.8")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin  "This game was played in South Africa, around 1931CE.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
)
