(define "PiecesCaptured" (count at:(handSite #1)))

//------------------------------------------------------------------------------

(game "Vai Lung Thlan" 
    (players 2)  
    (equipment { 
        (mancalaBoard 2 6 store:None
            (track "Track" "5,W,N,E" loop:true)
        )
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
        (piece "Seed" Shared)
        (hand Each)
    })  
    
    (rules 
        (start (set Count 5 to:(sites Track)))		  
        (play 
            (move
                Select
                (from 
                    (sites Mover) 
                    if:(< 0 (count at:(from)))
                )
                (then 
                    (sow 
                        if:(= (count at:(to)) 1)
                        apply:(fromTo 
                            (from (to)) 
                            (to (handSite Mover)) 
                            count:(count at:(to))
                        )
                        backtracking:true
                    )
                )
            )
        )
        (end
            (if (no Moves Mover)
                (byScore {
                    (score P1 ("PiecesCaptured" P1)) 
                    (score P2 ("PiecesCaptured" P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Vai Lung Thlān is a two-row mancala-style board game played by the Mizo people of India, Bangladesh, and Myanmar.")
        (aliases {"Vai Lung Thlān"})
        (rules "2x6 board. Five counters in each hole. Sowing occurs from any hole in the player's row in a clockwise direction. If the final counter lands into an empty hole, it is captured. If there is an unbroken line of holes behind it also having single counters, these are also captured. The player who captures the most counters wins.")
        (source "Shakespear 1912: 39.")
        (version "1.0.8")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)
