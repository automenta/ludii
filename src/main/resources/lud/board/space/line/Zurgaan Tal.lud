(define "RemoveEnemyPieceNotInLine3" 
    (move
        Remove
        (forEach 
            (sites Occupied by:Next container:"Board") 
            if:(not (is Line 3 Orthogonal through:(site)))
        )
    )
)

(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Zurgaan Tal"
    (players 2)
    
    (equipment 
        { 
        (board
            (add
                (merge {
                    
                    (add
                        (merge {
                            (remove (hex 1) edges:{{1 0} {2 0} {2 4} {4 5} {5 3} {3 1}})
                            (rotate 30 (scale 0.87 (remove (hex 1) edges:{{1 0} {2 0} {2 4} {4 5} {5 3} {3 1}})))
                        })
                        edges:{{1 9} {9 3} {3 11} {11 5} {5 10} {10 4} {4 8} {8 2} {2 6} {6 0} {0 7} {7 1}}
                    )
                    
                    (scale 2 (add
                            (merge {
                                (remove (hex 1) edges:{{1 0} {2 0} {2 4} {4 5} {5 3} {3 1}})
                                (rotate 30 (scale 0.87 (remove (hex 1) edges:{{1 0} {2 0} {2 4} {4 5} {5 3} {3 1}})))
                            })
                            edges:{{1 9} {9 3} {3 11} {11 5} {5 10} {10 4} {4 8} {8 2} {2 6} {6 0} {0 7} {7 1}}
                    ))
                    
                    (scale 3 (add
                            (merge {
                                (remove (hex 1) edges:{{1 0} {2 0} {2 4} {4 5} {5 3} {3 1}})
                                (rotate 30 (scale 0.87 (remove (hex 1) edges:{{1 0} {2 0} {2 4} {4 5} {5 3} {3 1}})))
                            })
                            edges:{{1 9} {9 3} {3 11} {11 5} {5 10} {10 4} {4 8} {8 2} {2 6} {6 0} {0 7} {7 1}}
                    ))
                })
                edges:{
                {25 13} {13 1} {9 21} {21 33} {27 15} {15 3} {11 23} {23 35} {29 17} {17 5} {10 22} {22 34}
                {28 16} {16 4} {32 20} {20 8} {2 14} {14 26} {30 18} {18 6} {0 12} {12 24} {7 19} {19 31}
                }
            )
            use:Vertex
        ) 
        (hand Each)
        (piece "Ball" Each 
            (move 
                Step 
                (to if:(is Empty (to))) 
                "IfLine3MoveAgain" 
            )
        ) 
        }
    )  
    (rules 
        (start (place "Ball" "Hand" count:17))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain" 
                    )  
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (forEach Piece)
                )
            )
        )
        }
        
        (end (if ("NoPiece" Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Zurgaan Tal is a game of alignment from Mongolia played on a hexagonal board.")
        (rules "Three concentric hexagons, with lines connecting the corners and midpoints of the sides. Seventeen pieces per player. Players alternate turns placing one of their pieces on an empty spot on the board. When all of the pieces are placed, they alternate turns moving one of their pieces to an empty adjacent spot along the lines of the board. During either phase, when a player places three of their pieces in a row, they may remove one of the opponent's pieces from the board. Pieces which are in a three-in-a-row arrangement cannot be removed from the board. The player who captures all of the opponent's pieces wins.")
        (source "Popova 1974: 26-32.")
        (version "1.0.8")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Mongolia, from around 1963CE to 1966CE.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
)

