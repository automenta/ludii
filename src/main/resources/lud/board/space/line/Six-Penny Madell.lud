(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3 Orthogonal exact:true) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Six-Penny Madell"
    (players 2)  
    (equipment 
        {
        (board
            (add
                (merge {
                    (shift 2 1.16 
                        (remove
                            (tri 3)
                            edges:{{1 4} {4 3} {1 3}}
                        )
                    )
                    (shift 1 0.58 
                        (scale 2 
                            (remove
                                (tri 3)
                                edges:{{1 4} {4 3} {1 3}}
                            )
                        )
                    )
                    (scale 3 
                        (remove
                            (tri 3)
                            edges:{{1 4} {4 3} {1 3}}
                        )
                    )
                })
                edges:{{0 6} {6 12} {2 8} {8 14} {5 11} {11 17}}
            )
            use:Vertex
        )
        (hand Each)
        (piece "Ball" Each
            (move
                Step
                (to if:(is Empty (to))) 
                "IfLine3MoveAgain"
            )
        )
        }
    )  
    (rules 
        (start (place "Ball" "Hand" count:6))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    (move
                        Remove
                        (sites Occupied by:Next container:"Board") 
                    )
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain"
                    )  
                )
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    (move
                        Remove
                        (sites Occupied by:Next container:"Board") 
                    )
                    (forEach Piece)
                )
            )
        )
        }
        (end 
            (if 
                ("NoPiece" Next) 
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Six-Penny Madell is an alignment game played in Wiltshire, England during the nineteenth century, and perhaps older. They were typically drawn on a floor or in the ground, and the pieces were whatever was readily available, such as stones or sticks.")
        (rules "Three concentric triangles, with lines connecting their corners. Six pieces per player. Pieces can move or be placed where the lines intersect, or in the center of the sides of the triangles. Players alternate turns placing their pieces on an empty space on the board.When all the pieces have been placed, players alternate turns moving their pieces to an empty adjacent space along the lines. During either phase, when a player places three pieces in a row along one of the sides of one of the lines, they remove one of the pieces belonging to the other player. The player who removes all of the opponent's pieces wins.")
        (source "Murray 1951: 43.")
        (version "1.0.8")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in England, around 1897CE.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
        (piece Scale "Ball" 0.55)
    })
    
)

