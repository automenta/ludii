(define "RemoveEnemyPieceNotInLine3" 
    (move
        Remove
        (forEach 
            (sites Occupied by:Next container:"Board") 
            if:(not (is Line 3 Orthogonal through:(site)))
        )
    )
)

(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3 Orthogonal) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Gurgaldaj"
    (players 2)  
    (equipment 
        {
        (board
            (add
                (merge {
                    (shift 2 1.16 
                        (remove
                            (tri 3)
                            edges:{{1 4} {4 3} {1 3}}
                        )
                    )
                    (shift 1 0.58 
                        (scale 2 
                            (remove
                                (tri 3)
                                edges:{{1 4} {4 3} {1 3}}
                            )
                        )
                    )
                    (scale 3 
                        (remove
                            (tri 3)
                            edges:{{1 4} {4 3} {1 3}}
                        )
                    )
                })
                edges:{
                {0 6} {6 12} {2 8} {8 14} {5 11} {11 17}
                {3 9} {9 15} {1 7} {7 13} {4 10} {10 16}
                }
            )
            use:Vertex
        )
        (hand Each)
        (piece "Ball" Each
            (move
                Step
                (to if:(is Empty (to))) 
                "IfLine3MoveAgain"
            )
        )
        }
    )  
    (rules 
        (start (place "Ball" "Hand" count:8))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    ("RemoveEnemyPieceNotInLine3")
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain"
                    )  
                )
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    ("RemoveEnemyPieceNotInLine3")
                    (forEach Piece)
                )
            )
        )
        }
        (end 
            (if 
                ("NoPiece" Next) 
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Gurgaldaj (Nightingale) is a game of alignment played on a triangular board in Mongolia. ")
        (aliases {"Altan Gurgaldai"})
        (rules "Three concentric triangles, with lines connecting their midpoints and corners. Eight pieces per player. Players alternate turns placing a piece on an empty spot on the board. When all of the pieces are placed, players alternate turns moving a piece to an empty adjacent spot along the lines. When a player places three of their pieces in a row, they remove one of the opponent's pieces from the board. A piece that is in a three-in-a-row pattern cannot be removed. The player who removes all of the opponent's pieces wins.")
        (source "Popova 1974: 26-32.")
        (version "1.0.8")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Mongolia, from around 1963CE to 1991CE.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
)

